use super::ast; // super instead of self because lalrpop wraps this in an internal module
use super::spans;


grammar(ctx: &mut spans::SpanMaker<'input>);

Ident: String = <r"[a-z_]\w*"> => String::from(<>);
Tag: String = <r"`[A-Z0-9]\w*"> => String::from(<>);

IntLiteral: String = {
    <r"-?(?:0|[1-9][0-9]*)"> => String::from(<>),
    r"0\d" "Numbers can`t have leading zeros" => "0".to_string(),
};
FloatLiteral: String =
    <r"-?(?:0|[1-9][0-9]*)\.[0-9]*(?:[eE]-?[0-9]+)?"> => String::from(<>);
StringLiteral: String =
    <r#""[^\\"\n\r]*(?:\\[tn'"\\][^\\"\n\r]*)*""#> => String::from(<>);

CommentRest = {
    "*)",
    "Sorry, comments can`t contain *s unless you`re closing them like *)",
};
Comment = {
    <r#"\(\*[^*]*"#> CommentRest,
    r#"//.*"#,
};


// make sure __proto__ is not considered a valid identifier
Illegal = "__proto__";

SepList<T, Sep>: Vec<T> = {
    <v:(<T> Sep)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};
SepListOpt<T, Sep>: Vec<T> = {
    SepList<T, Sep>,
    => Vec::new(),
};

Spanned<T>: spans::Spanned<T> = {
    <l: @L> <val: T> <r: @R> => (val, ctx.span(l, r))
};

VarOrLiteral: Box<ast::Expr> = {
    Spanned<Ident> => Box::new(
        match <>.0.as_str() {
            "false" | "true" => ast::Expr::Literal(ast::Literal::Bool, <>),
            "null" => ast::Expr::Literal(ast::Literal::Null, <>),
            _ => ast::Expr::Variable(<>)
        }
    ),

    Spanned<FloatLiteral> => Box::new(ast::Expr::Literal(ast::Literal::Float, <>)),
    Spanned<IntLiteral> => Box::new(ast::Expr::Literal(ast::Literal::Int, <>)),
    Spanned<StringLiteral> => Box::new(ast::Expr::Literal(ast::Literal::Str, <>)),
}

If: Box<ast::Expr> = {
    "if" <Spanned<Expr>> "then" <Expr> "else" <Expr> => Box::new(ast::Expr::If(<>)),
}


KeyPairPattern: (spans::Spanned<String>, Box<ast::LetPattern>) = {
    Comment* <name: Spanned<Ident>> "=" <pat: LetPattern> => (name, Box::new(pat)),
    Comment* <name: Spanned<Ident>> => (name.clone(), Box::new(ast::LetPattern::Var(name.0))),
}
LetPattern: ast::LetPattern = {
    <Ident> => ast::LetPattern::Var(<>),
    "{" <SepList<KeyPairPattern, ";">> "}" => ast::LetPattern::Record(<>),
}

FuncSub = "fun" <LetPattern> "->" <Expr>;
FuncDef: Box<ast::Expr> = {
    Spanned<FuncSub> => Box::new(ast::Expr::FuncDef(<>)),
}
Call: Box<ast::Expr> = {
    Spanned<RefExpr> CallExpr => {
        let ((lhs, span), rhs) = (<>);
        Box::new(ast::Expr::Call(lhs, rhs, span))
    }
}


RecordExtension = {
    <CallExpr> "with"
}
KeyPairExpr: (spans::Spanned<String>, Box<ast::Expr>) = {
    Comment* <Spanned<Ident>> "=" <Expr>,
    Comment* <name: Spanned<Ident>> => (name.clone(), Box::new(ast::Expr::Variable(name))),
}
RecordSub = "{" <RecordExtension?> <SepListOpt<KeyPairExpr, ";">> "}";
Record: Box<ast::Expr> = {
    Spanned<RecordSub> => {
        let ((proto, fields), span) = <>;
        Box::new(ast::Expr::Record(proto, fields, span))
    }
}
FieldAccess: Box<ast::Expr> = {
    <lhs: SimpleExpr> <rhs: Spanned<("." Ident)>> => Box::new(ast::Expr::FieldAccess(lhs, (rhs.0).1, rhs.1)),
}

Case: Box<ast::Expr> = {
    <Spanned<Tag>> <CallExpr> => Box::new(ast::Expr::Case(<>)),
}

MatchPattern: ast::MatchPattern = {
    Tag Ident => ast::MatchPattern::Case(<>),
    Ident => ast::MatchPattern::Wildcard(<>),
}
MatchArm = {
    Comment* "|" <Spanned<MatchPattern>> "->" <CompareExpr>,
}
MatchSub = "match" <Spanned<Expr>> "with" <MatchArm+>;
Match: Box<ast::Expr> = {
    MatchSub => {
        let ((param, span), arms) = <>;
        Box::new(ast::Expr::Match(param, arms, span))
    }
}


LetLHS = {
    "let" <Ident> "=" <Expr>,
}
LetRHS = {
    "in" <Expr>,
}
Let: Box<ast::Expr> = {
    <LetLHS> <LetRHS> => Box::new(ast::Expr::Let(<>)),
}


LetRecDef = {
    <Ident> "=" Comment* <FuncDef>,
}
LetRecLHS = {
    "let" "rec" <SepList<LetRecDef, "and">>,
}
LetRec: Box<ast::Expr> = {
     <LetRecLHS> <LetRHS> => Box::new(ast::Expr::LetRec(<>)),
}


NewRef: Box<ast::Expr> = {
    Spanned<("ref" CallExpr)> => {
        let ((_, expr), span) = <>;
        Box::new(ast::Expr::NewRef(expr, span))
    }
}
RefGet: Box<ast::Expr> = {
    "!" <Spanned<RefExpr>> => Box::new(ast::Expr::RefGet(<>))
}
RefSet: Box<ast::Expr> = {
    <Spanned<CallExpr>> ":=" <Expr> => Box::new(ast::Expr::RefSet(<>))
}


MultOpSub: (ast::OpType, ast::Op) = {
    "*" => (ast::OpType::IntOp, ast::Op::Mult),
    "/" => (ast::OpType::IntOp, ast::Op::Div),
    "%" => (ast::OpType::IntOp, ast::Op::Rem),
    "*." => (ast::OpType::FloatOp, ast::Op::Mult),
    "/." => (ast::OpType::FloatOp, ast::Op::Div),
    "%." => (ast::OpType::FloatOp, ast::Op::Rem),
}
MultOp: Box<ast::Expr> = {
    Spanned<(Spanned<MultExpr> MultOpSub Spanned<CallExpr>)> => {
        let ((lhs, op, rhs), span) = <>;
        Box::new(ast::Expr::BinOp(lhs, rhs, op.0, op.1, span))
    },
}
AddOpSub: (ast::OpType, ast::Op) = {
    "+" => (ast::OpType::IntOp, ast::Op::Add),
    "-" => (ast::OpType::IntOp, ast::Op::Sub),
    "+." => (ast::OpType::FloatOp, ast::Op::Add),
    "-." => (ast::OpType::FloatOp, ast::Op::Sub),
    "^" => (ast::OpType::StrOp, ast::Op::Add),
}
AddOp: Box<ast::Expr> = {
    Spanned<(Spanned<AddExpr> AddOpSub Spanned<MultExpr>)> => {
        let ((lhs, op, rhs), span) = <>;
        Box::new(ast::Expr::BinOp(lhs, rhs, op.0, op.1, span))
    },
}
CmpOpSub: (ast::OpType, ast::Op) = {
    "<" => (ast::OpType::IntOrFloatCmp, ast::Op::Lt),
    "<=" => (ast::OpType::IntOrFloatCmp, ast::Op::Lte),
    ">" => (ast::OpType::IntOrFloatCmp, ast::Op::Gt),
    ">=" => (ast::OpType::IntOrFloatCmp, ast::Op::Gte),

    "==" => (ast::OpType::AnyCmp, ast::Op::Eq),
    "!=" => (ast::OpType::AnyCmp, ast::Op::Neq),
}
CmpOp: Box<ast::Expr> = {
    Spanned<(Spanned<AddExpr> CmpOpSub Spanned<AddExpr>)> => {
        let ((lhs, op, rhs), span) = <>;
        Box::new(ast::Expr::BinOp(lhs, rhs, op.0, op.1, span))
    },
}










RecordTypeExtension = <Type> "with";
KeyPairType = {
    <Spanned<Ident>> ":" <Type>,
}
RecordTypeSub = "{" <RecordTypeExtension?> <SepList<KeyPairType, ";">> "}";
RecordType: Box<ast::TypeExpr> = {
    Spanned<RecordTypeSub> => {
        let ((ext, fields), span) = <>;
        Box::new(ast::TypeExpr::Record(ext, fields, span))
    }
}

CaseTypeExtension = <Type> "|";
VariantType = <Spanned<Tag>> "of" <NoFunType>;
CaseTypeSub = "[" <CaseTypeExtension?> <SepList<VariantType, "|">> "]";
CaseType: Box<ast::TypeExpr> = {
    Spanned<CaseTypeSub> => {
        let ((ext, cases), span) = <>;
        Box::new(ast::TypeExpr::Case(ext, cases, span))
    }
}
FuncTypeSub = <NoFunType> "->" <Type>;
FuncType: Box<ast::TypeExpr> = {
    Spanned<FuncTypeSub> => Box::new(ast::TypeExpr::Func(<>)),
}
RefReadability: ast::Readability = {
    "readonly" "ref" => ast::Readability::ReadOnly,
    "writeonly" "ref" => ast::Readability::WriteOnly,
    "ref" => ast::Readability::ReadWrite,
}
RefType: Box<ast::TypeExpr> = {
    NoFunType Spanned<RefReadability> => Box::new(ast::TypeExpr::Ref(<>)),
}
QMark: spans::Span = Spanned<"?"> => <>.1;
NullableType: Box<ast::TypeExpr> = {
    NoFunType QMark => Box::new(ast::TypeExpr::Nullable(<>)),
}

TypeVar = "'" <Ident>;

NoFunType: Box<ast::TypeExpr> = {
    Spanned<Ident> => Box::new(ast::TypeExpr::Ident(<>)),
    <NoFunType> "as" <Spanned<TypeVar>> => Box::new(ast::TypeExpr::Alias(<>)),
    Spanned<TypeVar> => Box::new(ast::TypeExpr::TypeVar(<>)),

    RecordType,
    CaseType,
    RefType,
    NullableType,
    "(" <Type> ")",
}
Type: Box<ast::TypeExpr> = {
    NoFunType,
    FuncType,
}






SimpleExpr = {
    FieldAccess,
    Record,
    VarOrLiteral,
    "(" <Expr> ")",
    "(" <Expr> ":" <Type> ")" => Box::new(ast::Expr::Typed(<>)),
}
RefExpr = {
    SimpleExpr,
    RefGet,
}
CallExpr = {
    RefExpr,
    Call,
    Case,
    NewRef,
}
MultExpr = {
    CallExpr,
    MultOp,
}
AddExpr = {
    MultExpr,
    AddOp,
}
CompareExpr = {
    AddExpr,
    CmpOp,
}
TopLevelExpr = {
    CompareExpr,
    FuncDef,
    If,
    Let,
    LetRec,
    Match,
    RefSet,
}
Expr = {
    TopLevelExpr,
    Comment <Expr>,
}

TopLevelItem: ast::TopLevel = {
    <LetLHS> => ast::TopLevel::LetDef(<>),
    <LetRecLHS> => ast::TopLevel::LetRecDef(<>),
    <TopLevelExpr> => ast::TopLevel::Expr(*<>),

    Comment <TopLevelItem>,
}

pub Script = {
   <SepList<TopLevelItem, ";">>
}
